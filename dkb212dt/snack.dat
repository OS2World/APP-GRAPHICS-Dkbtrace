{ DKB scene description file SNACK.DAT  }
{ File originally written by Tom Price  }

INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"

VIEW_POINT
  LOCATION <0.0 50.0 -150.0>
  UP <0.0 1.0 0.0>
  RIGHT <1.3333 0.0 0.0>
  LOOK_AT <0.0 0.0 0.0>
END_VIEW_POINT


OBJECT
  SPHERE <0.0 0.0 0.0> 20.0 END_SPHERE
  TRANSLATE <50.0 150.0 -250.0>
    TEXTURE
      COLOUR White
      AMBIENT 1.0
      DIFFUSE 0.0
    END_TEXTURE
  LIGHT_SOURCE
  COLOUR White
END_OBJECT

OBJECT
  SPHERE <0.0 0.0 0.0> 20.0 END_SPHERE
    TRANSLATE <-50.0 150.0 -250.0>
    TEXTURE
      COLOUR White
      AMBIENT 1.0
      DIFFUSE 0.0
    END_TEXTURE
    LIGHT_SOURCE
    COLOUR White
END_OBJECT


{The Sky}
OBJECT
  SPHERE <0.0 -49000.0 -200.0> 50000.0 INVERSE END_SPHERE
    TEXTURE 
      Cloud_Sky
      0.05 
      SCALE <5000.0 1000.0 5000.0>
      AMBIENT 0.7
      DIFFUSE 0.0
    END_TEXTURE
    COLOUR SkyBlue
END_OBJECT

{ The wood grain tabletop }
OBJECT
  INTERSECTION
    PLANE <1.0 0.0 0.0> 1.0 END_PLANE
    PLANE <1.0 0.0 0.0> -1.0 INVERSE END_PLANE
    PLANE <0.0 1.0 0.0> 1.0 END_PLANE
    PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
    PLANE <0.0 0.0 1.0> 1.0 END_PLANE
    PLANE <0.0 0.0 1.0> -1.0 INVERSE END_PLANE
  END_INTERSECTION
    TEXTURE
      Pine_Wood
      SCALE <7.0 1.0 0.7>
      ROTATE <0.0 -30.0 0.0>
      AMBIENT 0.1
      DIFFUSE 0.5
      REFLECTION 0.3
      BRILLIANCE 3.0
    END_TEXTURE
  BOUNDED_BY
    INTERSECTION
      PLANE <1.0 0.0 0.0> 1.01 END_PLANE
      PLANE <1.0 0.0 0.0> -1.01 INVERSE END_PLANE
      PLANE <0.0 1.0 0.0> 1.01 END_PLANE
      PLANE <0.0 1.0 0.0> -1.01 INVERSE END_PLANE
      PLANE <0.0 0.0 1.0> 1.01 END_PLANE
      PLANE <0.0 0.0 1.0> -1.01 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
  SCALE <200.0 1.0 200.0>
  TRANSLATE <0.0 -42.0 0.0>
END_OBJECT


{ a salami }
DECLARE
Salami = COMPOSITE
 { the outside skin }
  OBJECT
    UNION
      INTERSECTION
        QUADRIC Cylinder_X SCALE <1.0 20.0 20.0> END_QUADRIC
	PLANE <1.0 0.0 0.0> 10.0 END_PLANE
	PLANE <1.0 0.0 0.0> -10.0 INVERSE END_PLANE
      END_INTERSECTION
      SPHERE <-10.0 0.0 0.0> 20.0 END_SPHERE
    END_UNION
    TEXTURE
      0.05
      COLOUR RED 0.5 GREEN 0.2 BLUE 0.2
      AMBIENT 0.1
      DIFFUSE 0.8
      REFLECTION 0.2
      BRILLIANCE 3.0
      PHONG 0.3
      PHONGSIZE 20.0
    END_TEXTURE
    COLOUR RED 0.5 GREEN 0.2 BLUE 0.2
  END_OBJECT
  OBJECT
    INTERSECTION
      QUADRIC Cylinder_X SCALE <1.0 19.0 19.0> END_QUADRIC
      PLANE <1.0 0.0 0.0> 10.01 END_PLANE
      PLANE <1.0 0.0 0.0> -10.0 INVERSE END_PLANE
    END_INTERSECTION
    TEXTURE
      0.1
      COLOUR Pink
      AMBIENT 0.3
      DIFFUSE 0.7
    END_TEXTURE
    COLOUR Pink
  END_OBJECT
  BOUNDED_BY
    INTERSECTION
      QUADRIC Cylinder_X SCALE <1.0 20.01 20.01> END_QUADRIC
      PLANE <1.0 0.0 0.0> 10.02 END_PLANE
      PLANE <1.0 0.0 0.0> -30.01 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
END_COMPOSITE

{ a salami slice }
DECLARE
Slice = COMPOSITE
 { the outside skin }
  OBJECT
    INTERSECTION
      QUADRIC Cylinder_X SCALE <1.0 20.0 20.0> END_QUADRIC
      PLANE <1.0 0.0 0.0> 0.5 END_PLANE
      PLANE <1.0 0.0 0.0> -0.5 INVERSE END_PLANE
    END_INTERSECTION
    TEXTURE
      0.05
      COLOUR RED 0.5 GREEN 0.2 BLUE 0.2
      AMBIENT 0.1
      DIFFUSE 0.8
      REFLECTION 0.2
      BRILLIANCE 3.0
      PHONG 0.3
      PHONGSIZE 20.0
    END_TEXTURE
    COLOUR RED 0.5 GREEN 0.2 BLUE 0.2
  END_OBJECT
    OBJECT
      INTERSECTION
        QUADRIC Cylinder_X SCALE <1.0 19.0 19.0> END_QUADRIC
        PLANE <1.0 0.0 0.0> 0.51 END_PLANE
        PLANE <1.0 0.0 0.0> -0.51 INVERSE END_PLANE
      END_INTERSECTION
      TEXTURE
        0.1
        AMBIENT 0.3
        DIFFUSE 0.7
        COLOUR Pink
      END_TEXTURE
      COLOUR Pink
    END_OBJECT
  BOUNDED_BY
    INTERSECTION
      QUADRIC Cylinder_X SCALE <1.0 20.01 20.01> END_QUADRIC
      PLANE <1.0 0.0 0.0> 0.52 END_PLANE
      PLANE <1.0 0.0 0.0> -0.52 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
END_COMPOSITE


{ a wedge of cheese}
DECLARE
Cheese = OBJECT
  INTERSECTION
    QUADRIC Cylinder_Y SCALE <10.0 1.0 10.0> END_QUADRIC
    PLANE <0.0 1.0 0.0> 20.0 END_PLANE
    PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
    PLANE <1.0 0.0 0.0>  5.0 ROTATE <0.0 30.0 0.0> END_PLANE
    PLANE <1.0 0.0 0.0> -5.0 ROTATE <0.0 -30.0 0.0> INVERSE END_PLANE
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.5>
      ROTATE <0.0 -20.0 30>
      TRANSLATE <0.0 10.0 0.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 2.0 1.5>
      ROTATE <0.0 20.0 -30>
      TRANSLATE <0.0 12.0 -4.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.0>
      TRANSLATE <0.0 15.0 -9.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 2.0 1.5>
      ROTATE <0.0 -30.0 -30.0>
      TRANSLATE <0.0 15.0 5.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.5 1.5>
      ROTATE <0.0 0.0 -20.0>
      TRANSLATE <0.0 7.0 -9.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.0>
      ROTATE <0.0 10.0 10.0>
      TRANSLATE <0.0 10.0 -2.0>
    INVERSE END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.2>
      ROTATE <0.0 -10.0 0.0>
      TRANSLATE <0.0 5.0 0.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.3 1.0>
      TRANSLATE <0.0 3.0 -3.0>
      INVERSE
    END_QUADRIC
  END_INTERSECTION
  TEXTURE
    0.05
    COLOUR RED 1.0 GREEN 0.8 BLUE 0.0
    AMBIENT 0.2
    DIFFUSE 0.8
  END_TEXTURE
  COLOUR RED 1.0 GREEN 0.8 BLUE 0.0
  BOUNDED_BY
    INTERSECTION
      QUADRIC Cylinder_Y SCALE <10.01 1.0 10.01> END_QUADRIC
      PLANE <0.0 1.0 0.0> 20.01 END_PLANE
      PLANE <0.0 1.0 0.0> -0.01 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
END_OBJECT

{ a slice of cheese}
DECLARE
CheeseSlice = OBJECT
  INTERSECTION
    QUADRIC Cylinder_Y SCALE <10.0 1.0 10.0> END_QUADRIC
    PLANE <0.0 1.0 0.0> 20.0 END_PLANE
    PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
    PLANE <1.0 0.0 0.0>  0.2  END_PLANE
    PLANE <1.0 0.0 0.0> -0.2 INVERSE END_PLANE
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.5>
      ROTATE <0.0 -20.0 30>
      TRANSLATE <0.0 10.0 0.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 2.0 1.5>
      ROTATE <0.0 20.0 -30>
      TRANSLATE <0.0 12.0 -4.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.0>
      TRANSLATE <0.0 15.0 -9.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 2.0 1.5>
      ROTATE <0.0 -30.0 -30.0>
      TRANSLATE <0.0 15.0 5.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.5 1.5>
      ROTATE <0.0 0.0 -20.0>
      TRANSLATE <0.0 7.0 -9.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.0>
      ROTATE <0.0 10.0 10.0>
      TRANSLATE <0.0 10.0 -2.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.0 1.2>
      ROTATE <0.0 -10.0 0.0>
      TRANSLATE <0.0 3.0 5.0>
      INVERSE
    END_QUADRIC
    QUADRIC Cylinder_X SCALE <1.0 1.3 1.0>
      ROTATE <0.0 0.0 0.0>
      TRANSLATE <0.0 2.0 2.0>
      INVERSE
    END_QUADRIC
  END_INTERSECTION
  TEXTURE
    0.05
    COLOUR RED 1.0 GREEN 0.8 BLUE 0.0
    AMBIENT 0.2
    DIFFUSE 0.8
  END_TEXTURE
  COLOUR RED 1.0 GREEN 0.8 BLUE 0.0
  BOUNDED_BY
    INTERSECTION
      QUADRIC Cylinder_Y SCALE <10.01 1.0 10.01> END_QUADRIC
      PLANE <0.0 1.0 0.0> 20.01 END_PLANE
      PLANE <0.0 1.0 0.0> -0.01 INVERSE END_PLANE
      PLANE <1.0 0.0 0.0> 0.3 END_PLANE
      PLANE <1.0 0.0 0.0> -0.3 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
END_OBJECT

{ An oval glass dish }
DECLARE
Dish = OBJECT
  UNION
    INTERSECTION
      QUADRIC Sphere SCALE <100.0 25.0 25.0> END_QUADRIC
      QUADRIC Sphere SCALE <95.0 24.0 24.0> INVERSE END_QUADRIC
      PLANE <0.0 1.0 0.0> 0.0 END_PLANE
      PLANE <0.0 1.0 0.0> -5.0 INVERSE END_PLANE
    END_INTERSECTION
    INTERSECTION
      QUADRIC Cylinder_Y SCALE <95.0 1.0 24.0> END_QUADRIC
      PLANE <0.0 1.0 0.0> -4.0 END_PLANE
      PLANE <0.0 1.0 0.0> -5.0 INVERSE END_PLANE
    END_INTERSECTION
  END_UNION
  TEXTURE
    COLOUR Clear
    AMBIENT 0.1
    DIFFUSE 0.8
    REFRACTION 0.95
    IOR 1.5
    REFLECTION 0.05
    BRILLIANCE 2.0
  END_TEXTURE
  COLOUR Clear
  BOUNDED_BY
    INTERSECTION
      QUADRIC Cylinder_Y SCALE <100.01 1.0 25.01> END_QUADRIC
      PLANE <0.0 1.0 0.0> 0.01 END_PLANE
      PLANE <0.0 1.0 0.0> -5.01 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
END_OBJECT

{ a slice of bread }
DECLARE
BreadSlice = COMPOSITE
  OBJECT
    UNION
      INTERSECTION
        PLANE <1.0 0.0 0.0> 10.0 END_PLANE
	PLANE <1.0 0.0 0.0> -10.0 INVERSE END_PLANE
	PLANE <0.0 1.0 0.0> 1.0 END_PLANE
	PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
	PLANE <0.0 0.0 1.0> 10.0 END_PLANE
	PLANE <0.0 0.0 1.0> -10.0 INVERSE END_PLANE
      END_INTERSECTION
      INTERSECTION
        QUADRIC Cylinder_Y SCALE <11.0 1.0 7.0>
	  TRANSLATE <0.0 0.0 10.0>
	END_QUADRIC
	PLANE <0.0 1.0 0.0> 1.0 END_PLANE
	PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
      END_INTERSECTION
    END_UNION
    TEXTURE
      0.05
      COLOUR RED 0.4 GREEN 0.3 BLUE 0.1
      AMBIENT 0.3
      DIFFUSE 0.7
      REFLECTION 0.1
      BRILLIANCE 2.0
    END_TEXTURE
    COLOUR RED 0.4 GREEN 0.3 BLUE 0.1
  END_OBJECT

  OBJECT
    UNION
      INTERSECTION
        PLANE <1.0 0.0 0.0> 9.5 END_PLANE
	PLANE <1.0 0.0 0.0> -9.5 INVERSE END_PLANE
	PLANE <0.0 1.0 0.0> 1.01 END_PLANE
	PLANE <0.0 1.0 0.0> -1.01 INVERSE END_PLANE
	PLANE <0.0 0.0 1.0> 9.5 END_PLANE
	PLANE <0.0 0.0 1.0> -9.5 INVERSE END_PLANE
      END_INTERSECTION
      INTERSECTION
        QUADRIC Cylinder_Y SCALE <10.5 1.0 6.5>
	  TRANSLATE <0.0 0.0 10.0>
	END_QUADRIC
	PLANE <0.0 1.0 0.0> 1.01 END_PLANE
	PLANE <0.0 1.0 0.0> -1.01 INVERSE END_PLANE
      END_INTERSECTION
    END_UNION
    TEXTURE
      0.1
      COLOUR RED 0.7 GREEN 0.6 BLUE 0.45
      AMBIENT 0.3
      DIFFUSE 0.7
    END_TEXTURE
    COLOUR RED 0.7 GREEN 0.6 BLUE 0.45
  END_OBJECT
  BOUNDED_BY
    INTERSECTION
      PLANE <1.0 0.0 0.0> 10.1 END_PLANE
      PLANE <1.0 0.0 0.0> -10.1 INVERSE END_PLANE
      PLANE <0.0 1.0 0.0> 1.0 END_PLANE
      PLANE <0.0 1.0 0.0> -1.1 INVERSE END_PLANE
      PLANE <0.0 0.0 1.0> 17.1 END_PLANE
      PLANE <0.0 0.0 1.0> -10.1 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND
END_COMPOSITE	

{ Now to put the scene together}
COMPOSITE
  OBJECT Dish
    SCALE <0.7 1.0 1.2>
    TRANSLATE <0.0 -36.0 -25.0>
  END_OBJECT

  COMPOSITE Salami
    ROTATE <0.0 35.0 0.0>
    TRANSLATE <-30.0 -20.0 -20.0>
  END_COMPOSITE

  OBJECT Cheese
    SCALE <2.0 2.0 2.0>
    ROTATE <0.0 25.0 0.0>
    TRANSLATE <30.0 -40.0 -25.0>
  END_OBJECT
  TRANSLATE <0.0 0.0 25.0>
  ROTATE <0.0 -15.0 0.0>
  TRANSLATE <-35.0 0.0 20.0>
END_COMPOSITE

{ now a sandwich }
COMPOSITE
  COMPOSITE Slice
    ROTATE <0.0 90.0 0.0>
    ROTATE <90.0 0.0 0.0>
    TRANSLATE <45.0 -33.0 -35.0>
  END_COMPOSITE

  OBJECT CheeseSlice
    SCALE <1.0 2.0 2.0>
    ROTATE <0.0 -90.0 0.0>
    ROTATE <90.0 0.0 0.0>
    TRANSLATE <0.0 0.0 -16.0>
    ROTATE <0.0 30.0 0.0>
    TRANSLATE <45.0 -35.0 -35.0>
  END_OBJECT

  COMPOSITE BreadSlice
    SCALE <1.6 2.0 1.0>
    ROTATE <0.0 -150.0 0.0>
    TRANSLATE <45.0 -38.0 -35.0>
  END_COMPOSITE

  COMPOSITE BreadSlice
    SCALE <1.6 2.0 1.0>
    ROTATE <0.0 -150.0 0.0>
    TRANSLATE <45.0 -29.0 -35.0>
  END_COMPOSITE
  TRANSLATE <0.0 15.0 10.0>
END_COMPOSITE

{spotlight on the sandwich}
OBJECT
  INTERSECTION
    QUADRIC Cylinder_Y SCALE <7.5 1.0 7.5> END_QUADRIC
    QUADRIC Cylinder_Y SCALE <7.4 1.0 7.4> INVERSE END_QUADRIC
    PLANE <0.0 1.0 0.0> 50.0 END_PLANE
    PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
  END_INTERSECTION
  TRANSLATE <45.0 100.0 -25.0>
  TEXTURE
    COLOUR White
    AMBIENT 0.3
    DIFFUSE 0.7
  END_TEXTURE
  COLOUR White
END_OBJECT

OBJECT
  SPHERE <0.0 0.0 0.0> 4.0 END_SPHERE
  TRANSLATE <45.0 145.0 -25.0>
  TEXTURE
    COLOUR White
    AMBIENT 1.0
    DIFFUSE 0.0
  END_TEXTURE
  LIGHT_SOURCE
  COLOUR White
END_OBJECT

COMPOSITE Slice
  ROTATE <0.0 90.0 0.0>
  ROTATE <65.0 0.0 0.0>
  TRANSLATE <-30.0 25.0 45.0>
END_COMPOSITE

COMPOSITE Slice
  ROTATE <0.0 90.0 0.0>
  ROTATE <60.0 0.0 0.0>
  TRANSLATE <35.0 25.0 25.0>
END_COMPOSITE

OBJECT CheeseSlice
  SCALE <1.0 2.0 2.0>
  ROTATE <0.0 -90.0 0.0>
  ROTATE <50.0 0.0 0.0>
  TRANSLATE <-20.0 35.0 30.0>
END_OBJECT

OBJECT CheeseSlice
  SCALE <1.0 2.0 2.0>
  ROTATE <0.0 -90.0 0.0>
  ROTATE <70.0 0.0 0.0>
  TRANSLATE <65.0 15.0 35.0>
END_OBJECT

COMPOSITE BreadSlice
  SCALE <1.6 2.0 1.0>
  ROTATE <-40.0 -60.0 0.0>
  TRANSLATE <-60.0 25.0 35.0>
END_COMPOSITE

COMPOSITE BreadSlice
  SCALE <1.6 2.0 1.0>
  ROTATE <60.0 50.0 0.0>
  TRANSLATE <70.0 0.0 30.0>
END_COMPOSITE
