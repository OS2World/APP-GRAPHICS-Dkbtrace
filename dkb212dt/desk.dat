INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"

DECLARE
  RedPencil = COMPOSITE
    OBJECT
      INTERSECTION
	QUADRIC Cylinder_Y SCALE <0.5 1.0 0.5> END_QUADRIC
	PLANE <0.0 1.0 0.0> 30.0 END_PLANE
	PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
      END_INTERSECTION
      TEXTURE
        0.05
    	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR RED 1.0
      END_TEXTURE
     END_OBJECT

     OBJECT
       INTERSECTION
	QUADRIC Cylinder_Y SCALE <0.5 1.0 0.5> END_QUADRIC
	PLANE <0.0 1.0 0.0> 32.0 END_PLANE
	PLANE <0.0 1.0 0.0> 30.0 INVERSE END_PLANE
      END_INTERSECTION
      TEXTURE
        0.05
    	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR Tan
      END_TEXTURE
      END_OBJECT
   END_COMPOSITE

DECLARE
  GreenPencil = COMPOSITE
    OBJECT
      INTERSECTION
	QUADRIC Cylinder_Y SCALE <0.5 1.0 0.5> END_QUADRIC
	PLANE <0.0 1.0 0.0> 30.0 END_PLANE
	PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
      END_INTERSECTION
      TEXTURE
        0.05
    	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR GREEN 1.0
      END_TEXTURE
     END_OBJECT
     OBJECT
       INTERSECTION
	QUADRIC Cylinder_Y SCALE <0.5 1.0 0.5> END_QUADRIC
	PLANE <0.0 1.0 0.0> 32.0 END_PLANE
	PLANE <0.0 1.0 0.0> 30.0 INVERSE END_PLANE
      END_INTERSECTION
      TEXTURE
        0.05
    	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR Tan
      END_TEXTURE
      END_OBJECT
   END_COMPOSITE

DECLARE
  BluePencil = COMPOSITE
    OBJECT
      INTERSECTION
	QUADRIC Cylinder_Y SCALE <0.5 1.0 0.5> END_QUADRIC
	PLANE <0.0 1.0 0.0> 30.0 END_PLANE
	PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
      END_INTERSECTION
      TEXTURE
        0.05
    	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR BLUE 1.0
      END_TEXTURE
     END_OBJECT
     OBJECT
       INTERSECTION
	QUADRIC Cylinder_Y SCALE <0.5 1.0 0.5> END_QUADRIC
	PLANE <0.0 1.0 0.0> 32.0 END_PLANE
	PLANE <0.0 1.0 0.0> 30.0 INVERSE END_PLANE
      END_INTERSECTION
        TEXTURE
           0.05
	   AMBIENT 0.3
	   DIFFUSE 0.7
	   COLOUR Tan
        END_TEXTURE
      END_OBJECT
   END_COMPOSITE

VIEW_POINT
  	LOCATION <0.0 40.0 -150.0>
	UP <0.0 1.0 0.0>
	RIGHT <1.3333 0.0 0.0>
	LOOK_AT <0.0 0.0 0.0>
END_VIEW_POINT

{The back wall}

OBJECT
    PLANE <0.0 0.0 1.0> 200.0 END_PLANE
    TEXTURE
    	0.1
	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR LightGray
    END_TEXTURE
    COLOUR LightGray
END_OBJECT

{The ceiling}
OBJECT
    PLANE <0.0 1.0 0.0> 500.0 END_PLANE
    TEXTURE
    	0.1
	AMBIENT 0.3
	DIFFUSE 0.7
	COLOUR White
    END_TEXTURE
END_OBJECT


{The desk surface}

OBJECT
    INTERSECTION
        PLANE <0.0 1.0 0.0> 2.0 END_PLANE
	PLANE <0.0 1.0 0.0> -2.0 INVERSE END_PLANE
	PLANE <0.0 0.0 1.0> 100.0 END_PLANE
	PLANE <0.0 0.0 1.0> -100.0 INVERSE END_PLANE
	PLANE <1.0 0.0 0.0> 125.0 END_PLANE
	PLANE <1.0 0.0 0.0> -125.0 INVERSE END_PLANE
    END_INTERSECTION
    TRANSLATE <0.0 -20.0 0.0>
    TEXTURE
	0.05
	AMBIENT 0.4
	DIFFUSE 0.6
	Dark_Wood
	SCALE <5.0 1.0 1.0>
	REFLECTION 0.2
	BRILLIANCE 3.0
    END_TEXTURE
END_OBJECT

{Paperwork}
OBJECT
  UNION
    TRIANGLE
	<0.0 0.0 0.0>
	<8.5 0.0 0.0>
	<0.0 0.0 -11.0>
    END_TRIANGLE
    TRIANGLE
	<0.0 0.0 -11.0>
	<8.5 0.0 -11.0>
	<8.5 0.0 0.0>
    END_TRIANGLE
   END_UNION
   SCALE <3.0 1.0 3.0>
   ROTATE <0.0 -30.0 0.0>
   TRANSLATE <-20.0 -17.9 -40.0>

   TEXTURE
        0.05
	AMBIENT 0.1
	DIFFUSE 0.4
	COLOUR RED 0.5 GREEN 0.5 BLUE 0.3
   END_TEXTURE
   COLOUR RED 0.5 GREEN 0.5 BLUE 0.3
END_OBJECT

{A glass paperweight}
OBJECT
  INTERSECTION
	SPHERE <0.0 -5.0 0.0> 10.0 END_SPHERE
	PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
  END_INTERSECTION
  TRANSLATE <0.0 -17.8 -35.0>
  TEXTURE
	AMBIENT 0.1
	DIFFUSE 0.6
	COLOUR RED 0.3 GREEN 0.5 BLUE 0.3 ALPHA 1.0
	REFLECTION 0.1
	REFRACTION 1.0
	IOR 1.5
	BRILLIANCE 2.0
   END_TEXTURE
END_OBJECT

{The desk lamp}

COMPOSITE
  OBJECT
     INTERSECTION
      QUADRIC Cylinder_Y SCALE <3.0 1.0 3.0> END_QUADRIC
      PLANE <0.0 1.0 0.0> 40.0 END_PLANE
      PLANE <0.0 1.0 0.0> -18.0 INVERSE END_PLANE
     END_INTERSECTION
     TEXTURE
	Shiny
        0.05
	AMBIENT 0.2
        DIFFUSE 0.7
        COLOUR RED 0.7 GREEN 0.6 BLUE 0.1
     END_TEXTURE
  END_OBJECT

  OBJECT
    INTERSECTION
	QUADRIC Cylinder_Y SCALE <25.0 1.0 25.0> END_QUADRIC
	PLANE <0.0 1.0 0.0> 2.0 END_PLANE
	PLANE <0.0 1.0 0.0> -2.0 INVERSE END_PLANE
    END_INTERSECTION
    TRANSLATE <0.0 -16.0 -5.0>
    TEXTURE
	0.05
	AMBIENT 0.3
	DIFFUSE 0.6
	COLOUR RED 0.5 GREEN 0.4 BLUE 0.1
	REFLECTION 0.4
	BRILLIANCE 4.0
	BUMPS 0.1
    END_TEXTURE
  END_OBJECT

  OBJECT
   INTERSECTION
     QUADRIC Cylinder_X SCALE <1.0 10.0 10.0> END_QUADRIC
     QUADRIC Cylinder_X SCALE <1.0 9.95 9.95> INVERSE END_QUADRIC
     PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
     PLANE <1.0 0.0 0.0> -30.0 INVERSE END_PLANE
     PLANE <1.0 0.0 0.0> 30.0 END_PLANE
   END_INTERSECTION
	TRANSLATE <0.0 35.0 -13.0>
      TEXTURE
	Shiny
	0.05
	AMBIENT 0.5
	DIFFUSE 0.5
	COLOUR DarkGreen 
	REFLECTION 0.3
	BRILLIANCE 4.0
      END_TEXTURE
   END_OBJECT

   OBJECT
     UNION
	INTERSECTION
	   SPHERE <-30.0 35.0 -13.0> 10.0 END_SPHERE
	   SPHERE <-30.0 35.0 -13.0> 9.95 INVERSE END_SPHERE
	   PLANE <0.0 1.0 0.0> 35.0 INVERSE END_PLANE
	   PLANE <1.0 0.0 0.0> -30.0 END_PLANE
	END_INTERSECTION
	INTERSECTION
	   PLANE <0.0 1.0 0.0> 35.0 INVERSE END_PLANE
	   PLANE <1.0 0.0 0.0> 30.0 INVERSE END_PLANE
	   SPHERE <30.0 35.0 -13.0> 10.0 END_SPHERE
	   SPHERE <30.0 35.0 -13.0> 9.95 INVERSE END_SPHERE
	END_INTERSECTION
      END_UNION
      TEXTURE
	Shiny
	0.05
   	AMBIENT 0.2
	DIFFUSE 0.7
	COLOUR RED 0.7 GREEN 0.6 BLUE 0.1
	END_TEXTURE
   END_OBJECT


   ROTATE <0.0 35.0 0.0>
   TRANSLATE <50.0 0.0 30.0>
	
END_COMPOSITE
	
{The fluorescent tube inside the lamp}

OBJECT
   INTERSECTION
	QUADRIC Cylinder_X END_QUADRIC
	PLANE <1.0 0.0 0.0> -25.0 INVERSE END_PLANE
	PLANE <1.0 0.0 0.0>  25.0 END_PLANE
   END_INTERSECTION
   TRANSLATE <0.0 43.0 -10.0>
   ROTATE <0.0 35.0 0.0>
   TRANSLATE <50.0 0.0 30.0>
   TEXTURE
	COLOUR White
	AMBIENT 1.0
	DIFFUSE 0.0
   END_TEXTURE
   LIGHT_SOURCE
   COLOUR White
END_OBJECT

{The Picture itself}

COMPOSITE
  OBJECT
   INTERSECTION
	PLANE <1.0 0.0 0.0> 1.0 END_PLANE
	PLANE <1.0 0.0 0.0> -1.0 INVERSE END_PLANE
	PLANE <0.0 1.0 0.0> 1.0 END_PLANE
	PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
	PLANE <0.0 0.0 1.0> 1.0 END_PLANE
	PLANE <0.0 0.0 1.0> -1.0 INVERSE END_PLANE
   END_INTERSECTION
   TRANSLATE <1.0 1.0 1.0>
   SCALE <20.0 15.0 1.0>
   TEXTURE
	AMBIENT 0.05
	DIFFUSE 0.9
	IMAGEMAP <1.0 -1.0 0.0> IFF "waterbowl.ham" ONCE
	SCALE <40.0 30.0 1.0>
	SCALE <1.5 1.5 1.0>
   END_TEXTURE
  END_OBJECT

{ The picture frame }
  OBJECT
    UNION
     INTERSECTION
	UNION
	  QUADRIC Cylinder_Y SCALE <1.0 1.0 1.0> TRANSLATE <41.0 0.0 0.0> END_QUADRIC
	  QUADRIC Cylinder_Y SCALE <1.0 1.0 1.0> TRANSLATE <-1.0 0.0 0.0> END_QUADRIC
	END_UNION
	  PLANE <0.0 1.0 0.0> 31.0 END_PLANE
          PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
     END_INTERSECTION
     INTERSECTION
	UNION
  	  QUADRIC Cylinder_X SCALE <1.0 1.0 1.0> TRANSLATE <0.0 31.0 0.0> END_QUADRIC
	  QUADRIC Cylinder_X SCALE <1.0 1.0 1.0> TRANSLATE <0.0 -1.0 0.0> END_QUADRIC
	END_UNION
	  PLANE <1.0 0.0 0.0> 41.0 END_PLANE
	  PLANE <1.0 0.0 0.0> -1.0 INVERSE END_PLANE
     END_INTERSECTION
	  SPHERE <-1.0 -1.0 0.0> 1.0 END_SPHERE
	  SPHERE <-1.0 31.0 0.0> 1.0 END_SPHERE
	  SPHERE <41.0 -1.0 0.0> 1.0 END_SPHERE
	  SPHERE <41.0 31.0 0.0> 1.0 END_SPHERE
    END_UNION
    TEXTURE
	0.05
	Shiny
	COLOUR RED 0.6 GREEN 0.5 BLUE 0.1
	AMBIENT 0.3
	DIFFUSE 0.7
    END_TEXTURE
    COLOUR RED 0.6 GREEN 0.5 BLUE 0.1
  END_OBJECT

  SCALE <1.5 1.5 1.5>
  ROTATE <10.0 -35.0 0.0>
  TRANSLATE <-65.0 -15.0 -25.0>

END_COMPOSITE

{The pencil holder}
COMPOSITE
  OBJECT
    INTERSECTION
	QUADRIC Cylinder_Y SCALE <5.0 1.0 5.0> END_QUADRIC
	QUADRIC Cylinder_Y SCALE <4.8 1.0 4.8> INVERSE END_QUADRIC
	PLANE <0.0 1.0 0.0> 0.0 INVERSE END_PLANE
	PLANE <0.0 1.0 0.0> 15.0 ROTATE <-45.0 0.0 0.0> END_PLANE
    END_INTERSECTION
    TEXTURE
	0.05
	Shiny
	AMBIENT 0.3
	DIFFUSE 0.7
        COLOUR RED 0.7 GREEN 0.6 BLUE 0.1
	REFLECTION 0.4
	BRILLIANCE 6.0
    END_TEXTURE
  END_OBJECT
  COMPOSITE RedPencil
	ROTATE <0.0 0.0 -2.0>
	TRANSLATE <1.0 0.0 1.0>
  END_COMPOSITE
  COMPOSITE GreenPencil
	ROTATE <0.0 0.0 2.0>
	TRANSLATE <-1.0 3.0 0.0>
  END_COMPOSITE
  COMPOSITE BluePencil
	ROTATE <-2.0 0.0 3.0>
	TRANSLATE <0.0 -2.0 -1.0>
  END_COMPOSITE
  BOUNDED_BY
    INTERSECTION
	QUADRIC Cylinder_Y SCALE <6.0 1.0 6.0> END_QUADRIC
	PLANE <0.0 1.0 0.0> 36.0 END_PLANE
	PLANE <0.0 1.0 0.0> -4.0 INVERSE END_PLANE
    END_INTERSECTION
  END_BOUND

  ROTATE <0.0 45.0 0.0>
  TRANSLATE <70.0 -18.0 -20.0>

END_COMPOSITE

{The light source}

OBJECT
    SPHERE <0.0 0.0 0.0> 20.0 END_SPHERE
    TRANSLATE <20.0 100.0 -200.0>
    TEXTURE
	COLOUR White
	AMBIENT 0.3
	DIFFUSE 0.0
    END_TEXTURE
    LIGHT_SOURCE
    COLOUR White
END_OBJECT
