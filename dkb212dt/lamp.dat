{ DKB Data file LAMP.DAT originally by ?? }

{ This would have won the Intl. Obfuscated DKB Code Contest, So I }
{ reformatted it while converting it to DKB 2.10 :-)  All kidding }
{ aside, nice job, whoever wrote it!  -  Aaron A. Collins }

INCLUDE "colors.dat"
INCLUDE "shapes.dat"	{ contains the new "X_Disk" primitive }
INCLUDE "textures.dat"

VIEW_POINT
  LOCATION <0.0 -80.0 0.0>
  DIRECTION <0.0 1.0 0.0>
  UP <0.0 0.0 1.0>
  RIGHT <1.333333 0.0 0.0>
  TRANSLATE < 25.0 0.0 5.0 >
  ROTATE < -30.0 0.0 30.0 >
END_VIEW_POINT

OBJECT
  SPHERE <0.0 0.0 0.0> 1.0 END_SPHERE
  TRANSLATE <44.514 0.0 13.5>
  TEXTURE
    COLOR White
    AMBIENT 1.0
    DIFFUSE 0.0
  END_TEXTURE
  LIGHT_SOURCE
  COLOR White
END_OBJECT

OBJECT
  SPHERE <0.0 0.0 0.0> 1.0 END_SPHERE
  TRANSLATE <-60.514 0.0 160.5>
  TEXTURE
    COLOR RED 0.7 GREEN 0.7 BLUE 0.7
    AMBIENT 1.0
    DIFFUSE 0.0
  END_TEXTURE
  LIGHT_SOURCE
  COLOR RED 0.7 GREEN 0.7 BLUE 0.7
END_OBJECT

OBJECT
  SPHERE < 0.0 0.0 0.0 > 5.0 END_SPHERE
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  SPHERE < 38.161 0.0 17.197 > 1.0 END_SPHERE
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  SPHERE < 9.307 0.0 30.288 > 1.0 END_SPHERE
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  INTERSECTION X_Disk END_INTERSECTION
  SCALE < 31.685 1.0 1.0 >
  ROTATE < 0.0 -72.9193 0.0 >
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT INTERSECTION X_Disk END_INTERSECTION
  SCALE < 31.685 1.0 1.0 >
  ROTATE < 0.0 -155.5969 0.0 >
  TRANSLATE < 38.161 0.0 17.197 >
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  INTERSECTION X_Disk END_INTERSECTION
  SCALE < 3.2 0.5 0.5 >
  TRANSLATE < 38.161 0.0 17.197 >
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  INTERSECTION
    QUADRIC
      Cone_Z
      SCALE < 1.0 1.0 1.0 >
      TRANSLATE < 44.514 0.0 17.685 >
    END_QUADRIC
    QUADRIC
      Cone_Z
      SCALE < 1.0 1.0 1.0 >
      TRANSLATE < 44.514 0.0 16.685 >
      INVERSE
    END_QUADRIC
    PLANE < 0.0 0.0 1.0 > 7.685 INVERSE END_PLANE
    PLANE < 0.0 0.0 1.0 > 13.695 END_PLANE
  END_INTERSECTION
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  INTERSECTION
    QUADRIC
      Cone_Z
      SCALE < 0.2 0.2 1.0 >
      TRANSLATE < 44.514 0.0 33.496 >
    END_QUADRIC
    QUADRIC
      Cone_Z
      SCALE < 0.2 0.2 1.0 >
      TRANSLATE < 44.514 0.0 32.496 >
      INVERSE
    END_QUADRIC
    PLANE < 0.0 0.0 1.0 > 13.695 INVERSE END_PLANE
    PLANE < 0.0 0.0 1.0 > 23.496 END_PLANE
  END_INTERSECTION
  TEXTURE Brass END_TEXTURE
END_OBJECT

OBJECT
  PLANE < 0.0 0.0 1.0 > -4.0 END_PLANE
  TEXTURE
    COLOR Red
    AMBIENT 0.4
    DIFFUSE 0.6
    BRILLIANCE 8.0
    REFLECTION 0.8
    PHONG 1.0
    PHONGSIZE 30.0
  END_TEXTURE
END_OBJECT
