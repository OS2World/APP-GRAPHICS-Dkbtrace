{ Window Highlighting DKB Scene by Aaron A. Collins }
{ This file is hereby released to the public domain. }

INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"

{ Someone to take in the breathtaking view... }

VIEW_POINT
   LOCATION <0.0  20.0  -100.0>
   DIRECTION <0.0 0.0  1.0>
   UP  <0.0  1.0  0.0>
   RIGHT <1.33333 0.0 0.0>
END_VIEW_POINT

{ Put down the beloved famous raytrace green/yellow checkered floor }

OBJECT
   PLANE <0.0 1.0 0.0> -10.0 END_PLANE
   TEXTURE
      CHECKER COLOUR Yellow COLOUR Green
      SCALE < 20.0 20.0 20.0 >
      AMBIENT 0.1
      DIFFUSE 0.9
   END_TEXTURE
   COLOUR Yellow
END_OBJECT

{
 Now a Blue Plastic sphere floating in space over the ground - note that no 
 Phong or specular reflection is given.  Any would conflict with the window
 "highlights" by showing that they are not exactly in the mirror direction!
}

OBJECT
   SPHERE <0.0 25.0 0.0> 40.0 END_SPHERE
   TEXTURE
      COLOUR Blue
      REFLECTION 0.8
      AMBIENT 0.3
      DIFFUSE 0.7
   END_TEXTURE
   COLOUR Blue
END_OBJECT

{
 A wall with a window frame to block the light source and cast the shadows
}

OBJECT
  UNION
    TRIANGLE <-1000.0 -1000.0 0.0> <1000.0 4.0 0.0> <1000.0 -1000.0 0.0> END_TRIANGLE
    TRIANGLE <-1000.0 -1000.0 0.0> <1000.0 4.0 0.0> <-1000.0 4.0 0.0> END_TRIANGLE
    TRIANGLE <4.0 4.0 0.0> <-1000.0 21.0 0.0> <-1000.0 4.0 0.0> END_TRIANGLE
    TRIANGLE <4.0 4.0 0.0> <-1000.0 21.0 0.0> <4.0 21.0 0.0> END_TRIANGLE
    TRIANGLE <-1000.0 21.0 0.0> <1000.0 1000.0 0.0> <-1000.0 1000.0 0.0> END_TRIANGLE
    TRIANGLE <-1000.0 21.0 0.0> <1000.0 1000.0 0.0> <1000.0 21.0 0.0> END_TRIANGLE
    TRIANGLE <1000.0 4.0 0.0> <17.0 21.0 0.0> <1000.0 21.0 0.0> END_TRIANGLE
    TRIANGLE <1000.0 4.0 0.0> <17.0 21.0 0.0> <17.0 4.0 0.0> END_TRIANGLE
    TRIANGLE <4.0 12.0 0.0> <17.0 13.0 0.0> <4.0 13.0 0.0> END_TRIANGLE
    TRIANGLE <4.0 12.0 0.0> <17.0 13.0 0.0> <17.0 12.0 0.0> END_TRIANGLE
    TRIANGLE <10.0 21.0 0.0> <11.0 4.0 0.0> <11.0 21.0 0.0> END_TRIANGLE
    TRIANGLE <10.0 21.0 0.0> <11.0 4.0 0.0> <10.0 4.0 0.0> END_TRIANGLE
  END_UNION
  TRANSLATE <39.0 89.0 -120.0>
  TEXTURE
    COLOUR Black
    AMBIENT 1.0
    DIFFUSE 0.0
  END_TEXTURE
END_OBJECT

{
  Now, the 4 actual "panes" to be reflected back onto the sphere for psuedo-
  "highlights".  They are not exactly co-incident with where the actual light
  source is, because they would block the light.  They are very near by where
  the openings are in the black wall above, close enough to give the proper
  illusion.  This is massive cheating, but then, this isn't reality, you see.
}

OBJECT
  UNION
    TRIANGLE <4.0 21.0 0.0> <10.0 13.0 0.0> <10.0 21.0 0.0> END_TRIANGLE
    TRIANGLE <4.0 21.0 0.0> <10.0 13.0 0.0> <4.0 13.0 0.0> END_TRIANGLE

    TRIANGLE <11.0 21.0 0.0> <17.0 13.0 0.0> <11.0 13.0 0.0> END_TRIANGLE
    TRIANGLE <11.0 21.0 0.0> <17.0 13.0 0.0> <17.0 21.0 0.0> END_TRIANGLE

    TRIANGLE <4.0 12.0 0.0> <10.0 4.0 0.0> <4.0 4.0 0.0> END_TRIANGLE
    TRIANGLE <4.0 12.0 0.0> <10.0 4.0 0.0> <10.0 12.0 0.0> END_TRIANGLE

    TRIANGLE <11.0 12.0 0.0> <17.0 4.0 0.0> <11.0 4.0 0.0> END_TRIANGLE
    TRIANGLE <11.0 12.0 0.0> <17.0 4.0 0.0> <17.0 12.0 0.0> END_TRIANGLE
  END_UNION
  SCALE <15.0 15.0 15.0>
  TRANSLATE <20.0 90.0 -100.0>
  TEXTURE
    COLOUR White
    AMBIENT 1.0
    DIFFUSE 0.0
  END_TEXTURE
END_OBJECT


{ A Light above the sphere, behind the camera and window frame for shadows }

OBJECT
   SPHERE <0.0 0.0 0.0> 0.001 END_SPHERE
   TRANSLATE <50.0 111.0 -130.0>
   TEXTURE
     COLOUR White
     AMBIENT 1.0
     DIFFUSE 0.0
   END_TEXTURE
   LIGHT_SOURCE
   COLOUR White
END_OBJECT
