{BOWL.DAT}

INCLUDE "colors.dat"
INCLUDE "shapes.dat"
INCLUDE "textures.dat"

VIEW_POINT
   LOCATION <0.0 10.0 -90.0>
   DIRECTION <0.0 0.0  1.0>
   UP  <0.0 1.0 0.0>
   RIGHT <1.0 0.0 0.0>
END_VIEW_POINT

{Top part of bowl}
OBJECT
    INTERSECTION
       SPHERE <0.0 0.0 0.0> 20.0 END_SPHERE
       SPHERE <0.0 0.0 0.0> 19.99 INVERSE END_SPHERE
       PLANE <0.0 1.0 0.0> 15.0 ROTATE <-10.0 0.0 0.0> END_PLANE
       PLANE <0.0 1.0 0.0> 10.0 ROTATE <-10.0 0.0 0.0> INVERSE END_PLANE
    END_INTERSECTION
    TRANSLATE <-8.0 0.0 0.0>
    TEXTURE
       COLOR LightGray ALPHA 0.7
       AMBIENT 0.3
       DIFFUSE 0.7
       REFRACTION 1.0
       IOR 1.5
    END_TEXTURE
END_OBJECT

{Bottom part of bowl}
OBJECT
   INTERSECTION
       SPHERE <0.0 0.0 0.0> 20.0 END_SPHERE
       SPHERE <0.0 0.0 0.0> 19.99 INVERSE END_SPHERE
       PLANE <0.0 1.0 0.0> 10.0 ROTATE <-10.0 0.0 0.0> END_PLANE
    END_INTERSECTION
    TRANSLATE <-8.0 0.0 0.0>
    TEXTURE
       AMBIENT 0.3
       DIFFUSE 0.7
       REFRACTION 0.5
    END_TEXTURE
END_OBJECT

OBJECT
    PLANE <0.0 1.0 0.0> -19.0 END_PLANE
    TEXTURE
       CHECKER COLOUR DarkSlateGrey COLOUR IndianRed
       SCALE <10.0 10.0 10.0>
    END_TEXTURE
END_OBJECT

OBJECT
    PLANE <0.0 0.0 1.0> 30.0 END_PLANE
    TEXTURE
       COLOUR RED 0.329804 BLUE 0.204314 GREEN 0.204314
    END_TEXTURE
END_OBJECT

OBJECT
   SPHERE <0.0 0.0 0.0> 1.5 END_SPHERE
   TRANSLATE <-100.0  100.0  -130.0>
   TEXTURE
      COLOUR RED 1.0 GREEN 1.0 BLUE 1.0
      AMBIENT 1.0
      DIFFUSE 0.0
   END_TEXTURE
   COLOUR RED 1.0 GREEN 1.0 BLUE 1.0
   LIGHT_SOURCE
END_OBJECT


{Picture}
COMPOSITE
  OBJECT
   INTERSECTION
	PLANE <1.0 0.0 0.0> 1.0 END_PLANE
	PLANE <1.0 0.0 0.0> -1.0 INVERSE END_PLANE
	PLANE <0.0 1.0 0.0> 1.0 END_PLANE
	PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
	PLANE <0.0 0.0 1.0> 1.0 END_PLANE
	PLANE <0.0 0.0 1.0> -1.0 INVERSE END_PLANE
   END_INTERSECTION
    TRANSLATE <1.0 1.0 1.0>
    SCALE <20.0 15.0 1.0>
    TEXTURE
       COLOR White
       AMBIENT 0.05
       DIFFUSE 0.9
    END_TEXTURE
  END_OBJECT

{ The picture frame }
  OBJECT
   UNION
    INTERSECTION
	UNION
	  QUADRIC Cylinder_Y SCALE <1.0 1.0 1.0> TRANSLATE <41.0 0.0 0.0> END_QUADRIC
	  QUADRIC Cylinder_Y SCALE <1.0 1.0 1.0> TRANSLATE <-1.0 0.0 0.0> END_QUADRIC
	END_UNION
	  PLANE <0.0 1.0 0.0> 31.0 END_PLANE
          PLANE <0.0 1.0 0.0> -1.0 INVERSE END_PLANE
    END_INTERSECTION
    INTERSECTION
	UNION
  	  QUADRIC Cylinder_X SCALE <1.0 1.0 1.0> TRANSLATE <0.0 31.0 0.0> END_QUADRIC
	  QUADRIC Cylinder_X SCALE <1.0 1.0 1.0> TRANSLATE <0.0 -1.0 0.0> END_QUADRIC
	END_UNION
	  PLANE <1.0 0.0 0.0> 41.0 END_PLANE
	  PLANE <1.0 0.0 0.0> -1.0 INVERSE END_PLANE
    END_INTERSECTION
	  SPHERE <-1.0 -1.0 0.0> 1.0 END_SPHERE
	  SPHERE <-1.0 31.0 0.0> 1.0 END_SPHERE
	  SPHERE <41.0 -1.0 0.0> 1.0 END_SPHERE
	  SPHERE <41.0 31.0 0.0> 1.0 END_SPHERE
   END_UNION
   TEXTURE
        Pine_Wood
	AMBIENT 0.3
	DIFFUSE 0.7
   END_TEXTURE
  END_OBJECT

    SCALE <1.5 1.5 1.5>
    TRANSLATE <20.0 20.0 29.0>
END_COMPOSITE
