INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"

VIEW_POINT
   DIRECTION <0.0 0.0  1.0>
   UP  <0.0  1.0  0.0>
   RIGHT <1.333 0.0 0.0>
   TRANSLATE <0.0 0.0 -56.0>
END_VIEW_POINT

OBJECT
   INTERSECTION
      SPHERE <0.0 0.0 0.0> 1.0 END_SPHERE
      SPHERE <0.0 0.0 0.0> 0.9 END_SPHERE
      PLANE  <0.0 1.0 0.0> 0.5 END_PLANE
   END_INTERSECTION
   BOUNDED_BY
      SPHERE <0.0 0.0 0.0> 21.0 END_SPHERE
   END_BOUND
   SCALE < 20.0 20.0 20.0 >
   TEXTURE
      0.05
      COLOUR Red
      AMBIENT 0.3
      DIFFUSE 0.8
      REFLECTION 0.1
   END_TEXTURE
   COLOUR Red
END_OBJECT

OBJECT
   INTERSECTION
      SPHERE <0.0 0.0 0.0> 1.0 END_SPHERE
      PLANE <0.0 1.0 0.0> 0.49 END_PLANE
   END_INTERSECTION
   BOUNDED_BY
      SPHERE <0.0 0.0 0.0> 21.0 END_SPHERE
   END_BOUND
   SCALE < 19.5 19.5 19.5 >
   TEXTURE
       RIPPLES 0.5
       FREQUENCY 100.0
       SCALE <100.0 100.0 100.0>
       REFLECTION 0.6
       REFRACTION 0.6
       IOR 1.2
   END_TEXTURE
   COLOUR Grey
END_OBJECT


OBJECT
   PLANE <0.0 1.0 0.0> -20.0 END_PLANE

   TEXTURE
      0.05
      Tan_Wood
      ROTATE <-45.0 0.0 0.0>
      SCALE <15.0 8.0 8.0>
      AMBIENT 0.3
      DIFFUSE 0.8
      REFLECTION 0.1
   END_TEXTURE
   COLOUR Tan
END_OBJECT

OBJECT
   PLANE <0.0 0.0 1.0> 100.0 END_PLANE

   TEXTURE
      Red_Marble
      SCALE <100.0 100.0 100.0>
      AMBIENT 0.3
      DIFFUSE 0.8
      REFLECTION 0.1
   END_TEXTURE
   COLOR Pink
END_OBJECT

OBJECT
   PLANE <0.0 1.0 0.0> 150.0 END_PLANE

   TEXTURE
     COLOUR RED 0.5 GREEN 0.5 BLUE 1.0
     AMBIENT 0.3
     DIFFUSE 0.8
   END_TEXTURE
   COLOUR RED 0.5 GREEN 0.5 BLUE 1.0
END_OBJECT

OBJECT
   SPHERE <0.0 0.0 0.0> 5.0 END_SPHERE

   TRANSLATE <100.0  120.0  -130.0>
   TEXTURE
     COLOUR White
     AMBIENT 1.0
     DIFFUSE 0.0
   END_TEXTURE
   LIGHT_SOURCE
   COLOUR White
END_OBJECT
