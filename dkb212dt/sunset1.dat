{
  This is the "classic" SUNSET scene by David K. Buck.  I'm resurrecting it
  because I always thought it looked very realistic...  - Aaron A. Collins
}

INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"

VIEW_POINT
   LOCATION <0.0  100.0  200.0>
   DIRECTION <0.0  0.0  -1.0>
   UP <0.0  1.0  0.0>
   RIGHT <1.333333 0.0 0.0>
END_VIEW_POINT

{ Define the ocean surface }
OBJECT
   PLANE <0.0 1.0 0.0> -10.0 END_PLANE

   TEXTURE
      WAVES 0.06
      FREQUENCY 5000.0
      SCALE <1000.0 1000.0 1000.0>
      REFLECTION 1.0
   END_TEXTURE
   COLOUR RED 1.0 GREEN 0.3
END_OBJECT

{ Put a floor underneath to catch any errant waves from the ripples }
OBJECT
   PLANE <0.0 1.0 0.0> -11.0 END_PLANE

   TEXTURE
      0.05
      COLOUR RED 1.0 GREEN 0.3
      AMBIENT 1.0
      DIFFUSE 0.0
   END_TEXTURE
   COLOUR RED 1.0 GREEN 0.3
END_OBJECT

{ Now draw the sky, a distant rear wall }
OBJECT
   PLANE <0.0 0.0 1.0> -200.0 END_PLANE

   TEXTURE
      0.05
      COLOUR RED 1.0 GREEN 0.3
      AMBIENT 0.3
      DIFFUSE 0.7
   END_TEXTURE
   COLOUR RED 1.0 GREEN 0.3
END_OBJECT

{ Now to cast some light on the subject }
OBJECT
   SPHERE <0.0 0.0 0.0> 20.0 END_SPHERE
   TRANSLATE <0.0  30.0  -160.0>

   TEXTURE
     COLOUR RED 1.0 GREEN 0.6
     AMBIENT 1.0
     DIFFUSE 0.0
   END_TEXTURE

   LIGHT_SOURCE
   COLOUR RED 1.0 GREEN 0.6
END_OBJECT
