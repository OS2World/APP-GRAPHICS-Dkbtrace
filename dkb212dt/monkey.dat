{ DKB sample QUARTIC data file written by Alexander Enzmann }

INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"

DECLARE Basic_Saddle =
   QUARTIC
   < 0.0   0.0   0.0   1.0  0.0   0.0   0.0   0.0   0.0  0.0
     0.0   0.0  -3.0   0.0  0.0   0.0   0.0   0.0   0.0  0.0
     0.0   0.0   0.0   0.0  0.0   0.0   0.0   0.0   0.0  0.0
     0.0   0.0   0.0  -1.0  0.0 >
   END_QUARTIC

DECLARE Unit_Cube =
   INTERSECTION
      PLANE < 1  0  0> 1 TEXTURE COLOR Clear END_TEXTURE END_PLANE
      PLANE <-1  0  0> 1 TEXTURE COLOR Clear END_TEXTURE END_PLANE
      PLANE < 0  1  0> 1 TEXTURE COLOR Clear END_TEXTURE END_PLANE
      PLANE < 0 -1  0> 1 TEXTURE COLOR Clear END_TEXTURE END_PLANE
      PLANE < 0  0  1> 1 TEXTURE COLOR Clear END_TEXTURE END_PLANE
      PLANE < 0  0 -1> 1 TEXTURE COLOR Clear END_TEXTURE END_PLANE
   END_INTERSECTION

{ Monkey Saddle }
OBJECT
   INTERSECTION
     QUARTIC Basic_Saddle
       TEXTURE
         COLOR Red
	 SPECULAR 1.0
	 ROUGHNESS 0.05
	 AMBIENT 0.2
	 DIFFUSE 0.8
       END_TEXTURE
     END_QUARTIC
     INTERSECTION Unit_Cube SCALE <2 2 2> END_INTERSECTION
   END_INTERSECTION
   BOUNDED_BY
     INTERSECTION Unit_Cube SCALE <2.5 2.5 2.5> END_INTERSECTION
   END_BOUND
   ROTATE <0 20 0>
   ROTATE <-30 0 0>
END_OBJECT

VIEW_POINT
   LOCATION  <0.0  0.0 -10.0>
   RIGHT     <1.33 0.0   0.0>
   UP        <0.0  1.0   0.0>
   DIRECTION <0.0  0.0   1.0>
END_VIEW_POINT

OBJECT
   SPHERE <0 0 0> 1 END_SPHERE
   TRANSLATE <200 30 -300> 
   TEXTURE
     COLOUR White
     AMBIENT 1.0
     DIFFUSE 0.0
   END_TEXTURE
   LIGHT_SOURCE
   COLOUR White
END_OBJECT
